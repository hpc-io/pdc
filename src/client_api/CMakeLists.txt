 #------------------------------------------------------------------------------
# Include source and build directories
#------------------------------------------------------------------------------
set( LOCAL_INCLUDE_DIR
  ${PDC_INCLUDES_BUILD_TIME}
  ${PROJECT_BINARY_DIR}
  ${PDC_SOURCE_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_BINARY_DIR}
  ${PDC_SOURCE_DIR}/src/server/include
  ${PDC_SOURCE_DIR}/src/server/dablooms
  ${PDC_SOURCE_DIR}/src/client_api/include
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/include
  ${PDC_SOURCE_DIR}/src/client_api/pdc_region/include
  ${PDC_SOURCE_DIR}/src/client_api/pdc_query/include
  ${PDC_SOURCE_DIR}/src/client_api/pdc_transform/include
  ${PDC_SOURCE_DIR}/src/client_api/pdc_analysis/include
  ${PDC_SOURCE_DIR}/src/client_api/profiling/include
  ${PDC_SOURCE_DIR}/src/pdc_utils/include
)

include_directories(
  ${LOCAL_INCLUDE_DIR}
  ${MERCURY_INCLUDE_DIR}
  ${FASTBIT_INCLUDE_DIR}
)

#------------------------------------------------------------------------------
# External dependencies
#------------------------------------------------------------------------------
# profiling
#set(PDC_EXT_LIB_DEPENDENCIES pdcprof ${PDC_EXT_LIB_DEPENDENCIES})
set(PDC_EXT_INCLUDE_DEPENDENCIES ${CMAKE_CURRENT_SOURCE_DIR}/profiling)
set(PDC_EXPORTED_LIBS pdcprof)

# Mercury
find_package(MERCURY REQUIRED)
if(MERCURY_FOUND)
  message(STATUS "mercury dir ${MERCURY_DIR}")
  set(PDC_EXT_INCLUDE_DEPENDENCIES ${MERCURY_INCLUDE_DIR}
    ${PDC_EXT_INCLUDE_DEPENDENCIES}
    )
  set(PDC_EXT_LIB_DEPENDENCIES mercury ${PDC_EXT_LIB_DEPENDENCIES})
endif()

include_directories(${PDC_EXT_INCLUDE_DEPENDENCIES})

#------------------------------------------------------------------------------
# Configure module header files
#------------------------------------------------------------------------------
# Set unique vars used in the autogenerated config file (symbol import/export)
if(BUILD_SHARED_LIBS)
  set(PDC_BUILD_SHARED_LIBS 1)
  set(PDC_LIBTYPE SHARED)
else()
  set(PDC_BUILD_SHARED_LIBS 0)
  set(PDC_LIBTYPE STATIC)
endif()

if(PDC_ENABLE_TIMING)
    add_definitions(-DPDC_TIMING=1)
endif()

#------------------------------------------------------------------------------
# Set sources
#------------------------------------------------------------------------------
set(PDC_SRCS
  ${PDC_SOURCE_DIR}/src/client_api/pdc.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_client_connect.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/pdc_prop.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/pdc_cont.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/pdc_obj.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_analysis/pdc_analysis_and_transforms_connect.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_analysis/pdc_analysis_common.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_analysis/pdc_analysis.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_analysis/pdc_hist_pkg.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/pdc_mpi.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_query/pdc_query.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_region/pdc_region.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_region/pdc_region_transfer.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_transform/pdc_transform.c
  ${PDC_SOURCE_DIR}/src/client_api/pdc_transform/pdc_transforms_common.c
  ${PDC_SOURCE_DIR}/src/server/pdc_client_server_common.c
  ${PDC_SOURCE_DIR}/src/server/pdc_server_region_transfer.c
  ${PDC_SOURCE_DIR}/src/server/pdc_server_region_cache.c
  ${PDC_SOURCE_DIR}/src/server/pdc_server_region_transfer_metadata_query.c
  ${PDC_SOURCE_DIR}/src/pdc_utils/pdc_timing.c
  ${PDC_SOURCE_DIR}/src/pdc_utils/pdc_malloc.c
  ${PDC_SOURCE_DIR}/src/pdc_utils/pdc_interface.c
  ${PDC_SOURCE_DIR}/src/pdc_utils/pdc_region_utils.c
  )

  add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/profiling)

#------------------------------------------------------------------------------
# Libraries
#------------------------------------------------------------------------------
# PDC
set(PDC_BUILD_INCLUDE_DEPENDENCIES
  ${LOCAL_INCLUDE_DIR}
)

add_library(pdc ${PDC_SRCS})

target_include_directories(pdc
  PUBLIC  "$<BUILD_INTERFACE:${PDC_BUILD_INCLUDE_DEPENDENCIES}>"
          $<INSTALL_INTERFACE:${PDC_INSTALL_INCLUDE_INTERFACE}>
)

target_link_libraries(pdc
  ${PDC_EXPORTED_LIBS}
  ${PDC_EXT_LIB_DEPENDENCIES}
  -ldl
)
pdc_set_lib_options(pdc "pdc" ${PDC_LIBTYPE})

set(PDC_EXPORTED_LIBS pdc ${PDC_EXPORTED_LIBS})

#-----------------------------------------------------------------------------
# Specify project header files to be installed
#-----------------------------------------------------------------------------
set(PDC_HEADERS
  ${PDC_SOURCE_DIR}/src/client_api/include/pdc.h
  ${PDC_SOURCE_DIR}/src/client_api/include/pdc_public.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_analysis/include/pdc_analysis.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/include/pdc_cont.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/include/pdc_mpi.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/include/pdc_obj.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_region/include/pdc_region.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_obj/include/pdc_prop.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_query/include/pdc_query.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_region/include/pdc_region.h
  ${PDC_SOURCE_DIR}/src/client_api/pdc_transform/include/pdc_transform.h
  ${PROJECT_BINARY_DIR}/pdc_config_sys.h
  ${PROJECT_BINARY_DIR}/pdc_config.h
  )

#-----------------------------------------------------------------------------
# Add file(s) to CMake Install
#-----------------------------------------------------------------------------
install(
  FILES
    ${PDC_HEADERS}
  DESTINATION
    ${PDC_INSTALL_INCLUDE_DIR}
  COMPONENT
    headers
)

install(
  FILES
    ${PDC_HL_HEADERS}
  DESTINATION
    ${PDC_INSTALL_INCLUDE_DIR}
  COMPONENT
    headers
)

#-----------------------------------------------------------------------------
# Add Target(s) to CMake Install
#-----------------------------------------------------------------------------
install(
  TARGETS
    pdc
  EXPORT
    ${PDC_EXPORTED_TARGETS}
  LIBRARY DESTINATION ${PDC_INSTALL_LIB_DIR}
  ARCHIVE DESTINATION ${PDC_INSTALL_LIB_DIR}
  RUNTIME DESTINATION ${PDC_INSTALL_BIN_DIR}
)

#-----------------------------------------------------------------------------
# Add Target(s) to CMake Install for import into other projects
#-----------------------------------------------------------------------------
install(
  EXPORT
    ${PDC_EXPORTED_TARGETS}
  DESTINATION
    ${PDC_INSTALL_DATA_DIR}/cmake/pdc
  FILE
    ${PDC_EXPORTED_TARGETS}.cmake
)

#-----------------------------------------------------------------------------
# Export all exported targets to the build tree for use by parent project
#-----------------------------------------------------------------------------
if(NOT PDC_EXTERNALLY_CONFIGURED)
EXPORT (
  TARGETS
    ${PDC_EXPORTED_LIBS}
  FILE
    ${PDC_EXPORTED_TARGETS}.cmake
)
endif()

#------------------------------------------------------------------------------
# Set variables for parent scope
#------------------------------------------------------------------------------
# Used by config.cmake.build.in and Testing
set(PDC_INCLUDES_BUILD_TIME
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_BINARY_DIR}
  ${PDC_EXT_INCLUDE_DEPENDENCIES}
  PARENT_SCOPE
)

# Used by config.cmake.install.in
set(PDC_INCLUDES_INSTALL_TIME
  ${PDC_INSTALL_INCLUDE_DIR}
  ${PDC_EXT_INCLUDE_DEPENDENCIES}
  PARENT_SCOPE
)
